CMAKE_MINIMUM_REQUIRED(VERSION 3.0)
PROJECT(rnn2d)
ENABLE_TESTING()

OPTION(WITH_CUDA "Compile binaries with CUDA support" ON)
OPTION(WITH_TORCH "Compile with Torch bindings" ON)
OPTION(WITH_OPENMP "Compile with OpenMP support" ON)
OPTION(WITH_PERFTOOLS "Compile with Google Perftools support" OFF)
OPTION(WITH_TESTS "Compile tests" ON)
OPTION(WITH_BENCHMARKS "Compile benchmarks" ON)
SET(CUDA_ARCH_LIST "Auto" CACHE STRING "Compile with support for these CUDA architectures")

LIST(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")

FIND_PACKAGE(Threads REQUIRED)
FIND_PACKAGE(BLAS REQUIRED)
FIND_PACKAGE(Glog REQUIRED)
FIND_PACKAGE(OpenMP)
FIND_PACKAGE(CUDA)

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/include)
INCLUDE_DIRECTORIES(${GLOG_INCLUDE_DIRS})
SET(CUDA_PROPAGATE_HOST_FLAGS OFF)
SET(COMMON_LIBRARIES "${GLOG_LIBRARIES}" "${CMAKE_THREAD_LIBS_INIT}")

SET(CMAKE_CXX_FLAGS "-std=c++0x -pedantic -Wall")
IF(OPENMP_FOUND AND WITH_OPENMP)
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
ENDIF()


IF(CUDA_FOUND AND WITH_CUDA)
  IF(NOT COMMAND CUDA_SELECT_NVCC_ARCH_FLAGS)
    INCLUDE(${CMAKE_CURRENT_SOURCE_DIR}/cmake/select_compute_arch.cmake)
  ENDIF()

  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DWITH_CUDA")
  SET(CUDA_NVCC_FLAGS "-std=c++11 -lineinfo -Xcompiler -Wall,-std=c++0x")
  SET(CUDA_NVCC_FLAGS "${CUDA_NVCC_FLAGS} ${CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE}}")
  IF(CMAKE_CXX_COMPILER_VERSION VERSION_GREATER 5)
    SET(CUDA_NVCC_FLAGS "${CUDA_NVCC_FLAGS} -D_MWAITXINTRIN_H_INCLUDED -D_FORCE_INLINES")
  ENDIF()
  CUDA_SELECT_NVCC_ARCH_FLAGS(NVCC_FLAGS_EXTRA ${CUDA_ARCH_LIST})
  LIST(APPEND CUDA_NVCC_FLAGS ${NVCC_FLAGS_EXTRA})
ENDIF(CUDA_FOUND AND WITH_CUDA)

FIND_PACKAGE(Perftools)
IF(Perftools_FOUND AND WITH_PERFTOOLS)
  MESSAGE(STATUS "Google Perftools found! Linking with ${Perftools_LIBRARIES}")
  INCLUDE_DIRECTORIES(${Perftools_INCLUDE_DIRS})
  LIST(APPEND COMMON_LIBRARIES "${Perftools_LIBRARIES}")
  IF ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
    SET(CMAKE_CXX_FLAGS
      "${CMAKE_CXX_FLAGS} -fno-builtin-malloc -fno-builtin-calloc -fno-builtin-realloc -fno-builtin-free")
  ENDIF()
  IF (CUDA_FOUND)
    SET(CUDA_NVCC_FLAGS
      "${CUDA_NVCC_FLAGS} -Xcompiler -fno-builtin-malloc,-fno-builtin-calloc,-fno-builtin-realloc,-fno-builtin-free")
  ENDIF()
ENDIF()


ADD_SUBDIRECTORY(include/rnn2d)
ADD_SUBDIRECTORY(src)
IF(WITH_TESTS)
  ADD_SUBDIRECTORY(test)
ENDIF(WITH_TESTS)
IF(WITH_BENCHMARKS)
  ADD_SUBDIRECTORY(bench)
ENDIF(WITH_BENCHMARKS)
ADD_SUBDIRECTORY(torch)
